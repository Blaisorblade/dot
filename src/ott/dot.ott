metavar var, x, y, z ::= {{ com Variable }}
{{ coq nat }} {{ coq-equality }} {{ ocaml string }} {{ ocamlvar "[[var]]" }} {{ lex alphanum }}
metavar label ::= {{ com Label }}
{{ coq nat }} {{ coq-equality }} {{ ocaml string }} {{ ocamlvar "[[label]]" }} {{ lex alphanum }}
indexvar index, i, j, k, n ::=
{{ coq nat }} {{ ocaml int }} {{ lex numeral }}

grammar
vlabel, l :: vlabel_   ::= {{ com Value label }}
  | label _v           :: :: any {{ tex [[label]] }}

mlabel, m :: mlabel_   ::= {{ com Method Label }}
  | label _m           :: :: any {{ tex [[label]] }}

clabel, Lc {{ tex {L_c} }} {{ ocaml tLc }} :: clabel_ ::= {{ com class label }}
  | label _c           :: :: any {{ tex {[[label]]}_c }}

alabel, La {{ tex {L_a} }} {{ ocaml tLa }} :: alabel_ ::= {{ com abstract type label }}
  | label _a           :: :: any {{ tex {[[label]]}_a }}

v :: v_                ::= {{ com Value }}
  | x                  :: :: var {{ com variable }}
  | v : T              :: :: wid {{ com widening }}
  | ( v )              :: S:: paren {{ icho [[v]] }}

tm, t :: Tm_           ::= {{ com Term }}
  | x                  :: :: var {{ com value }}
  | t : T              :: :: wid {{ com widening }}
  | val x = new c ; t  :: :: new  (+ bind x in c +) (+ bind x in t +) {{ com new instance }}
  | t . l              :: :: vsel {{ com value selection }}
  | t m t'             :: :: msel {{ com method invocation }}
  | ( t )              :: S:: paren {{ icho [[t]] }}
  | [ t / x ] t'       :: M:: subst {{ icho (subst_tm [[t]] [[x]] [[t']]) }}
  | e [ t ]            :: M:: ctx {{ icho (appctx_ctx_tm [[e]] [[t]]) }}
  | fun ( x : T ) T' t :: :: lam
  | app t t'           :: :: app

path, p :: path_       ::= {{ com Path }}
  | x                  :: :: var
  | p : T              :: :: wid
  | p . l              :: :: vsel
  | ( p )              :: S:: paren {{ icho [[p]] }}

constr, c :: constr_ ::= {{ com Constructor }}
  | Tc { </ defi // , // i IN 0 .. n /> } :: :: any

def :: def_ ::= {{ com Definition }}
  | l = v              :: :: value
  | m ( x ) = t        :: :: method (+ bind x in t +)
  | [ t / x ] def      :: M:: subst {{ icho (subst_def [[t]] [[x]] [[def]]) }}

gamma, G {{ tex \Gamma }} {{ ocaml gamma }} :: gamma_ ::=  {{ coq list (var*tp) }} {{ com Environment }}
  | []                 :: :: nil
   {{ coq G_nil }}
  | G , x : T          :: :: cons
   {{ coq (cons ([[x]],[[T]]) [[G]]) }}

store, s :: store_     ::=  {{ coq list (var*constr) }} {{ com Store }}
  | []                 :: :: nil
   {{ coq s_nil }}
  | s , x |-> c        :: :: cons
   {{ coq (cons ([[x]],[[c]]) [[s]]) }}

tlabel, L {{ ocaml tL }} :: tlabel_   ::= {{ com Type label }}
  | label _c           :: :: class {{ tex {[[label]]}_c }}
  | label _a           :: :: abstract {{ tex {[[label]]}_a }}

tp, T {{ ocaml tyT }}, S {{ coq St }} {{ ocaml tyS }}, U {{ ocaml tyU }}, V {{ ocaml tyV }}, W {{ ocaml tyW }} :: tp_ ::= {{ com Type }}
  | p . L              :: :: tsel {{ com type selection }}
  | T { z => Dseq }    :: :: rfn (+ bind z in Dseq +) {{ com refinement }}
  | T /\ T'            :: :: and {{ com intersection type }}
  | T \/ T'            :: :: or {{ com union type }}
  | Top                :: :: top {{ com top type }}
  | Bottom             :: :: bot {{ com bottom type }}
  | ( T )              :: S:: paren {{ icho [[T]] }}
  | [ t / x ] T        :: M:: subst {{ icho (subst_tp [[t]] [[x]] [[T]]) }}

tpc, Tc {{ tex T_c }} {{ ocaml tyTc }}, Sc {{ tex S_c }} {{ ocaml tySc }} :: tpc_ ::= {{ com Concrete Type }}
  | p . Lc             :: :: sel
  | Tc { z => Dseq }   :: :: rfn
  | Tc /\ Tc'          :: :: and
  | Top                :: :: top
  | ( Tc )             :: S:: paren {{ icho [[Tc]] }}
  | [ t / x ] Tc       :: M:: subst {{ icho (subst_tpc [[t]] [[x]] [[Tc]]) }}

decl, D {{ ocaml decl }} :: decl_       ::= {{ com Declaration }}
  | L : S '..' U       :: :: type {{ com type declaration }}
  | l : T              :: :: value {{ com value declaration }}
  | m : S -> T         :: :: method {{ com method declaration }}
  | [ t / x ] D        :: M:: subst {{ icho (subst_decl [[t]] [[x]] [[D]]) }}

decls, Dseq {{ tex \overline{D} }} {{ ocaml seqD }} :: Decls_ ::=
  | {}                 :: S:: empty
   {{ icho (Decls_seq nil) }}
  | </ Di // , // i /> :: :: seq
  | Dseq /\ Dseq'      :: M:: and
   {{ icho [[Dseq]] }} % TODO
  | Dseq \/ Dseq'      :: M:: or
   {{ icho [[Dseq]] }} % TODO
  | DseqBottom         :: M:: bot {{ tex \overline{D_\bot} }}
   {{ icho (Decls_seq nil) }} % TODO

ctx, e :: ctx_         ::= {{ com Context }}
  | __ . l             :: :: vsel
  | __ m t             :: :: msel_l
  | v m __             :: :: msel_r
  | __ : T             :: :: wid

terminals :: terminals_ ::=
  | |->                :: :: mapsto {{ tex \mapsto }}
  | ->                 :: :: rightarrow {{ tex \rightarrow }}
  | -->                :: :: longrightarrow {{ tex \;\longrightarrow\; }}
  | path>              :: :: longrightarrow_path {{ tex \;\longrightarrow_p\; }}
  | =>                 :: :: Rightarrow {{ tex \Rightarrow }}
  | \/                 :: :: tand {{ tex \vee }}
  | /\                 :: :: tor {{ tex \wedge }}
  | Top                :: :: top {{ tex \top }}
  | Bottom             :: :: bot {{ tex \bot }}
  | __                 :: :: hole {{ tex [\,] }}
  | in                 :: :: in {{ tex \in }}
  | mem                :: :: mem {{ tex \ni }}
  | |-                 :: :: ts {{ tex \,\vdash\, }}
  | |=                 :: :: ms {{ tex \,\models\, }}
  | fn                 :: :: fn {{ tex \text{\sl fn} }}
  | notin              :: :: notin {{ tex \not\in }}
  | ==                 :: :: equiv {{ tex \equiv }}
  | downto             :: :: downto {{ tex \downarrow }}

formula :: formula_    ::=
  | judgement          :: :: judgement
  | def is def'        :: :: def_eq
    {{ coq ([[def]] = [[def']]) }}
  | x |-> c in s       :: :: store_lookup
    {{ coq (store_bound [[x]] [[c]] [[s]]) }}
  | x : T in G         :: :: gamma_lookup
    {{ coq (env_bound [[x]] [[T]] [[G]]) }}
  | x notin fn ( T )   :: :: not_free_check
    {{ coq (~In [[x]] (fv_tp [[T]])) }}
  | formula1 .. formulan :: :: dots

embed
{{ coq
Notation G_nil := (@nil (var*tp)).
Definition env_bound x T G :=
  exists G1, exists G2,
    (G = List.app G1 (List.cons (x,T) G2)) /\
    ~In x (List.map (@fst var tp) G1).
Notation s_nil := (@nil (var*constr)).
Definition store_bound x c s :=
  exists s1, exists s2,
    (s = List.app s1 (List.cons (x,c) s2)) /\
    ~In x (List.map (@fst var constr) s1).
}}

subrules
  Lc <:: L
  La <:: L
  v <:: tm
  v <:: path
  path  <:: tm
  tpc <:: tp

contextrules
  ctx _:: tm :: tm

substitutions
  single tm var :: subst

freevars
  tm var :: fv

defns
Job :: '' ::=

 defn
 v downto x :: ::nowid:: nowid_ {{ com Remove Widening }} by

 ---------- :: var
 x downto x

 v downto x
 -------------- :: wid
 v : T downto x

 defn
 propagate_widening_value s v l v' v'' :: ::propagate_widening_value:: propagate_widening_value_ {{ com Propagate Widening }} by

 ------------------------------------ :: var
 propagate_widening_value s x l v' v'

 [], s |- (v : T) mem l : T'
 ------------------------------------------------ :: wid
 propagate_widening_value s (v : T) l v' (v' : T')

 defn
 propagate_widening_method s v m v' x' t' t'' :: ::propagate_widening_method:: propagate_widening_method_ {{ com Propagate Widening }} by

 ---------------------------------------------------- :: var
 propagate_widening_method s x m v' x' t' ([v'/x']t')

 [], s |- (v : T) mem m : S' -> T'
 ------------------------------------------------------------------- :: wid
 propagate_widening_method s (v : T) m v' x' t' (([(v':S')/x']t') : T')

 defn
 t | s --> t' | s' :: ::reduce:: red_ {{ com Reduction }} by


 --------------------------------------- :: new
 val x = new c; t | s --> t | s, x |-> c

 x |-> Tc { </ defi // i /> } in s
 defi is l=v'
 v downto x
 propagate_widening_value s v l v' v''
 ------------------------------------- :: vsel
 v.l | s --> v'' | s

 x |-> Tc { </ defi // i /> } in s
 defi is m(x)=t'
 v downto x
 propagate_widening_method s v m v' x t' t''
 ------------------------------------------- :: msel
 v m v' | s --> t'' | s

 t | s --> t' | s'
 ----------------------- :: ctx
 e[t] | s --> e[t'] | s'

 defn
 G , s |- p path> p' :: :: path_reduce:: path_red_ {{ com Path Reduction }} by

 x |-> Tc { </ defi // i /> } in s
 defi is l=v'
 v downto x
 v' downto x'
 ---------------------------------- :: base
 G, s |- v.l path> x'

 --------------------- :: wid
 G, s |- p : T path> p

 G, s |- p path> p'
 ---------------------- :: rec
 G, s |- p.l path> p'.l

 defn
 G , s |- T == T' :: ::tp_eq:: tp_eq_ {{ com Type Equality }} by

 G, s |- T <: T'
 G, s |- T' <: T
 ---------------- :: any
 G, s |- T == T'

 defn
 G , s |- t : T :: ::typing:: typ_ {{ com Type Assignment }} by

 x : T in G
 ------------- :: var
 G, s |- x : T

 G, s |- t mem l : T
 --------------------- :: vsel
 G, s |- t.l : T

 G, s |- t1 mem m : S1 -> T1
 G, s |- t2 : T2
 G, s |- T2 == S1
 --------------------------- :: msel
 G, s |- t1 m t2 : T1

 G, s |- t : T'
 G, s |- T' <: T
 ----------------- :: wid
 G, s |- t : T : T

 x notin fn(T)
 G, s |- Tc wfe
 G, s |- Tc expands x </ Lj : Sj .. Uj // j/>, </ lk : Tk // k/>, </ mi : Vi -> Wi // i/>
 G, x : Tc, s |- t : T
 </ G, x : Tc, s |- Sj <: Uj // j/>
 </ G, x : Tc, s |- vk' : Tk' // k/>
 </ G, x : Tc, s |- Tk' == Tk // k/>
 </ G, x : Tc, s |- Vi wfe // i/>
 </ G, x : Tc, xi : Vi, s |- ti : Wi' // i/>
 </ G, x : Tc, s |- Wi' == Wi // i/>
 ------------------------------------------------------------------------------------------ :: new
 G, s |- val x = new Tc { </ lk=vk // k/>, </ mi(xi)=ti // i/> }; t : T

 defn
 G , s |- t mem D :: ::membership:: mem_ {{ com Membership }} by

 G, s |- p : T
 G, s |- T expands z </ Di // i />
 --------------------------------------- :: path
 G, s |- p mem [p/z] Di

 z notin fn(T)
 G, s |- t : T
 G, s |- T expands z </ Di // i />
 --------------------------------------- :: term
 G, s |- t mem Di

 defn
 G , s |- T expands z Dseq :: ::expansion:: exp_ {{com Expansion }} {{ tex [[G]], [[s]] [[|-]] [[T]] \prec_[[z]] [[Dseq]] }} by

 G, s |- T expands z Dseq'
 ----------------------------------------------- :: rfn
 G, s |- T { z => Dseq } expands z Dseq /\ Dseq'

 G, s |- T1 expands z Dseq1
 G, s |- T2 expands z Dseq2
 ----------------------------------------- :: and
 G, s |- T1 /\ T2 expands z Dseq1 /\ Dseq2

 G, s |- T1 expands z Dseq1
 G, s |- T2 expands z Dseq2
 ----------------------------------------- :: or
 G, s |- T1 \/ T2 expands z Dseq1 \/ Dseq2

 G, s |- p mem L : S .. U
 G, s |- U expands z Dseq
 -------------------------- :: sel
 G, s |- p.L expands z Dseq


 ------------------------ :: top
 G, s |- Top expands z {}


 ----------------------------------- :: bot
 G, s |- Bottom expands z DseqBottom

 defn
 G , s |- S <: T :: ::subtyping:: sub_ {{com Subtyping }} by


 -------------- :: refl
 G, s |- T <: T

 G, s |- S <: T
 G, s |- S expands z Dseq'
 G, z : S, s |- Dseq' <: Dseq
 ---------------------------- :: rfn_r
 G, s |- S <: T { z => Dseq }

 G, s |- T <: T'
 ----------------------------- :: rfn_l
 G, s |- T { z => Dseq } <: T'

 G, s |- p mem L : S .. U
 G, s |- S <: U
 G, s |- S' <: S
 ------------------------- :: tsel_r
 G, s |- S' <: p.L

 G, s |- p mem L : S .. U
 G, s |- S <: U
 G, s |- U <: U'
 ------------------------- :: tsel_l
 G, s |- p.L <: U'

 G, s |- T <: T1
 G, s |- T <: T2
 --------------------- :: and_r
 G, s |- T <: T1 /\ T2

 G, s |- Ti <: T
 --------------------- :: and_l
 G, s |- T1 /\ T2 <: T

 G, s |- T <: Ti
 --------------------- :: or_r
 G, s |- T <: T1 \/ T2

 G, s |- T1 <: T
 G, s |- T2 <: T
 --------------------- :: or_l
 G, s |- T1 \/ T2 <: T


 ---------------- :: top
 G, s |- T <: Top


 ------------------- :: bot
 G, s |- Bottom <: T

 G, s |- p1 path> p2
 G, s |- T <: p2.L
 -------------------- :: path_red
 G, s |- T <: p1.L

 defn
 G , s |- D <: D' :: ::decl_sub:: decl_sub_ {{com Declaration subsumption }} by

 G, s |- S' <: S
 G, s |- U <: U'
 --------------------------------- :: type
 G, s |- L : S .. U <: L : S' .. U'

 G, s |- T <: T'
 --------------------------------- :: value
 G, s |- l : T <: l : T'

 G, s |- S' <: S
 G, s |- T <: T'
 --------------------------------- :: method
 G, s |- m : S -> T <: m : S' -> T'

 defn
 G , s |- Dseq <: Dseq' :: ::decls_sub:: decls_sub by

 defn
 G , s |- T wf :: ::wf:: wf_ {{com Well-formed types }} by

 G, s |- T wfe
 G, z : T { z => Dseq }, s |- Dseq wf
 ------------------------------------ :: rfn
 G, s |- T { z => Dseq } wf

 G, s |- p mem L : S .. U
 G, s |- S wfe
 G, s |- U wfe
 ------------------------- :: tsel1
 G, s |- p.L wf

 G, s |- p mem L : Bottom .. U
 ----------------------------- :: tsel2
 G, s |- p.L wf

 G, s |- T wfe
 G, s |- T' wfe
 ------------------ :: and
 G, s |- T /\ T' wf

 G, s |- T wfe
 G, s |- T' wfe
 ------------------ :: or
 G, s |- T \/ T' wf


 ------------------ :: bot
 G, s |- Bottom wf


 -------------- :: top
 G, s |- Top wf

 defn
 G , s |- D wf :: ::decl_wf:: decl_wf_ {{com Well-formed declarations }} by

 G, s |- S wfe
 G, s |- U wfe
 --------------------------------- :: type
 G, s |- L : S .. U wf

 G, s |- T wfe
 --------------------------------- :: value
 G, s |- l : T wf

 G, s |- S wfe
 G, s |- T wfe
 --------------------------------- :: method
 G, s |- m : S -> T wf

 defn
 G , s |- Dseq wf :: ::decls_wf:: decls_wf by

 defn
 G , s |- T wfe :: ::wfe:: wfe_ {{com Well-formed and expanding types }} by

 G, s |- T wf
 G, s |- T expands z Dseq
 ------------------------ :: any
 G, s |- T wfe

 defn
 G |= s :: ::ok_ctx:: ok_ctx_ {{com Consistent Context }} by

